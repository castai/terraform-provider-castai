---
page_title: "CAST AI Provider"
description: |-
  Use CAST AI provider to onboard the cluster and manage resources supported by CAST AI.
---

# CAST AI Provider

CAST AI provider can be used to onboard your cluster and manage resources supported by CAST AI.

-> **Note** To use the provider, an API token first must be generated for your account at https://console.cast.ai/

## Provider configuration

Terraform 0.13 and later:

```terraform
terraform {
  required_providers {
    castai = {
      source  = "castai/castai"
      version = "2.0.0"
    }
  }
}

# Configure the provider
provider "castai" {
  api_token = "my-castai-api-token"
}
```

## Example Usage

```terraform
# Your EKS cluster access configuration.

provider "aws" {
  region = var.cluster_region
}

data "aws_eks_cluster" "cluster" {
  name = var.cluster_name
}

data "aws_eks_cluster_auth" "cluster" {
  name = var.cluster_name
}

provider "helm" {
  kubernetes {
    host                   = data.aws_eks_cluster.cluster.endpoint
    cluster_ca_certificate = base64decode(data.aws_eks_cluster.cluster.certificate_authority[0].data)
    token                  = data.aws_eks_cluster_auth.cluster.token
  }
}

data "aws_caller_identity" "current" {}

# Your CAST AI EKS configuration

provider "castai" {
  api_token = var.castai_api_token
}

resource "castai_eks_cluster" "this" {
  account_id = data.aws_caller_identity.current.account_id
  region     = var.cluster_region
  name       = var.cluster_name
}

resource "helm_release" "castai_agent" {
  name            = "castai-agent"
  repository      = "https://castai.github.io/helm-charts"
  chart           = "castai-agent"
  cleanup_on_fail = true

  set {
    name  = "provider"
    value = "eks"
  }
  set_sensitive {
    name  = "apiKey"
    value = castai_eks_cluster.this.cluster_token
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `api_token` (String) The token used to connect to CAST AI API.

### Optional

- `api_url` (String) CAST.AI API url.